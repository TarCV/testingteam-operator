/*
 * Copyright 2019 TarCV
 * Copyright 2015 Shazam Entertainment Limited
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with the License.
 *
 * You may obtain a copy of the License at http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the specific language governing permissions and limitations under the License.
 */

buildscript {
    ext.kotlin_version = '1.3.0'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'application'

mainClassName = 'com.github.tarcv.tongs.TongsCli'

sourceCompatibility = rootProject.sourceCompatibility
targetCompatibility = rootProject.sourceCompatibility

dependencies {
    compile project(':tongs-common')
    compile 'org.codehaus.groovy:groovy-all:2.4.15'
    compile(
            "com.beust:jcommander:$JCOMMANDER_VERSION",
            "org.slf4j:slf4j-log4j12:$SLF4J_VERSION",
            "com.google.code.gson:gson:$GSON_VERSION",
            'org.simpleframework:simple-xml:2.7.1',
            'com.madgag:animated-gif-lib:1.2',
    )

    compile('org.lesscss:lesscss:1.7.0.1.1') {
        exclude module: 'slf4j-simple'
    }

    testCompile("org.hamcrest:hamcrest-all:$HAMCREST_VERSION")
    testCompile("junit:junit:$JUNIT_VERSION") {
        exclude module:'hamcrest-core'
    }
    testCompile(
            "org.jmock:jmock:$JMOCK_VERSION",
            "org.jmock:jmock-junit4:$JMOCK_VERSION")
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

jar {
    manifest {
        attributes 'Implementation-Title': POM_NAME,
                'Implementation-Version': VERSION_NAME,
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion,
                'Main-class': mainClassName
    }
}

run {
    systemProperties System.getProperties()

    if(project.hasProperty('args')) {
        args project.args.split('\\s+')
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
