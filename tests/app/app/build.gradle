apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'com.github.tarcv.tongs'

android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.github.tarcv.tongstestapp"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments test_argument: 'default'
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "flavor"
    productFlavors {
        f1 {
            dimension "flavor"
            applicationIdSuffix ".f1"
        }
        f2 {
            dimension "flavor"
            applicationIdSuffix ".f2"
            testInstrumentationRunner "com.github.tarcv.test.f2.TestRunner"
            testInstrumentationRunnerArguments test_argument: 'args\"ForF2', filter: 'com.github.tarcv.test.F2Filter'
        }
    }

    splits {
        density {
            enable true

            reset()
            include "420dpi", "xxhdpi"
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    testImplementation 'junit:junit:4.12'
    testImplementation group: 'org.json', name: 'json', version: '20180130'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testImplementation group: 'org.apache.commons', name: 'commons-exec', version: '1.3'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test:rules:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

tongs {
    title = "Acceptance tests"

    testPackage = "com.github.tarcv.test"

    totalAllowedRetryQuota = 4
    retryPerTestCaseQuota = 2

    poolingStrategy {
        manual {
            groupings {
                emulators = [System.getenv("DEVICE1"), System.getenv("DEVICE2")]
            }
        }
    }

    plugins {
        if (Boolean.valueOf(System.getenv("CI_STUBBED"))) {
            deviceProviders = ["com.github.tarcv.tongs.plugin.android.StubDeviceProvider"]
        }
    }

    tongsIntegrationTestRunType = Boolean.valueOf(System.getenv("CI_STUBBED")) ?
            com.github.tarcv.tongs.TongsConfiguration.TongsIntegrationTestRunType.STUB_PARALLEL_TESTRUN
            : com.github.tarcv.tongs.TongsConfiguration.TongsIntegrationTestRunType.RECORD_LISTENER_EVENTS
}
